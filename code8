// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© BeikabuOyaji

//@version=5
strategy("ADX and DI for v5", overlay=false)

len = input.int(15, "Length")
th = input.int(20, "Threshold")

// True Range
TrueRange = math.max(math.max(high - low, math.abs(high - nz(close[1]))), math.abs(low - nz(close[1])))

// Directional Movement
DirectionalMovementPlus = (high - nz(high[1]) > nz(low[1]) - low) ? math.max(high - nz(high[1]), 0) : 0
DirectionalMovementMinus = (nz(low[1]) - low > high - nz(high[1])) ? math.max(nz(low[1]) - low, 0) : 0

// Smoothed values
var float SmoothedTrueRange = 0.0
SmoothedTrueRange := nz(SmoothedTrueRange[1]) - (nz(SmoothedTrueRange[1]) / len) + TrueRange

var float SmoothedDirectionalMovementPlus = 0.0
SmoothedDirectionalMovementPlus := nz(SmoothedDirectionalMovementPlus[1]) - (nz(SmoothedDirectionalMovementPlus[1]) / len) + DirectionalMovementPlus

var float SmoothedDirectionalMovementMinus = 0.0
SmoothedDirectionalMovementMinus := nz(SmoothedDirectionalMovementMinus[1]) - (nz(SmoothedDirectionalMovementMinus[1]) / len) + DirectionalMovementMinus

// DI & ADX
DIPlus = SmoothedDirectionalMovementPlus / SmoothedTrueRange * 100
DIMinus = SmoothedDirectionalMovementMinus / SmoothedTrueRange * 100
DX = math.abs(DIPlus - DIMinus) / (DIPlus + DIMinus) * 100
ADX = ta.sma(DX, len)

// Plots
plot(DIPlus, color=color.green, title="DI+")
plot(DIMinus, color=color.red, title="DI-")

// Inputs
oscPeriod = input.int(20, "Oscillator Length", minval = 1)
upColor = input.color(#00ffbb, "Up Color")
downColor = input.color(#ff1100, "Down Color")

// Amazing Oscillator Calculation
midpointPrice = hl2
shortSMA = ta.sma(midpointPrice, 5)
longSMA = ta.sma(midpointPrice, 34)
amazingOsc = shortSMA - longSMA

// RSI-like Calculation
rise = ta.rma(math.max(ta.change(amazingOsc), 0), oscPeriod)
fall = ta.rma(-math.min(ta.change(amazingOsc), 0), oscPeriod)
customRSI = (fall == 0 ? 100 : rise == 0 ? 0 : 100 - (100 / (1 + rise / fall))) - 50
opacityLevel = customRSI > 0 and customRSI > customRSI[1] or customRSI < 0 and customRSI < customRSI[1] ? 30 : 80
barColor = customRSI > 0 ? color.new(upColor, opacityLevel) : color.new(downColor, opacityLevel)

// Plots
plot(customRSI, "RSI Column", barColor, 1, plot.style_columns)
plot(customRSI, "RSI Line", customRSI > 0 ? color.new(upColor, 0) : color.new(downColor, 0))
plot(30, "Upper Threshold", color.from_gradient(customRSI, 0, 50, color.new(color.gray, 70), color.new(downColor, 0)), 3)
plot(-30, "Lower Threshold", color.from_gradient(customRSI, -50, 0, color.new(upColor, 0), color.new(color.gray, 70)), 3)
midline = plot(0, "Midline", color.gray)

// Crossover and Crossunder Characters
plotchar(ta.crossover(customRSI, -30) ? -52 : na, "Buy Signal", char = "x", color = upColor, location = location.absolute, size = size.tiny)
plotchar(ta.crossunder(customRSI, 30) ? 52 : na, "Sell Signal", char = "x", color = downColor, location = location.absolute, size = size.tiny)

// Alerts
alertcondition(ta.crossover(customRSI, -30), "Bullish Reversal", "Amazing Oscillator Bullish Reversal")
alertcondition(ta.crossunder(customRSI, 30), "Bearish Reversal", "Amazing Oscillator Bearish Reversal")

alertcondition(ta.crossover(customRSI, 0), "Bullish Trend Shift", "Amazing Oscillator Bullish Trend Shift")
alertcondition(ta.crossunder(customRSI, 0), "Bearish Trend Shift", "Amazing Oscillator Bearish Trend Shift")

alertcondition(ta.crossunder(customRSI, customRSI[1]) and customRSI > 0, "Weakening Bullish Trend", "Amazing Oscillator Bullish Trend is Weakening")
alertcondition(ta.crossover(customRSI, customRSI[1]) and customRSI < 0, "Weakening Bearish Trend", "Amazing Oscillator Bearish Trend is Weakening")

var int check = 0
bonusconditionbuy = customRSI[1] <= 0  and  customRSI > 0
bonusconditionsell = customRSI[1] > 0  and  customRSI <= 0
if (ta.crossover(DIPlus,DIMinus) or bonusconditionbuy) and customRSI > 0 and check == 0 and barstate.isconfirmed
    strategy.entry("Long", strategy.long)
    alert('{"SYMBOL":"' + syminfo.ticker + '","ACTION":"BUY","CHECK":"NO"}', alert.freq_once_per_bar_close)
    check := 1
if check == 1 and (ta.crossunder(DIPlus,DIMinus) or customRSI <= 0) and barstate.isconfirmed
    if (ta.crossunder(DIPlus,DIMinus) or bonusconditionsell) and customRSI <= 0
        strategy.close("Long")
        strategy.entry("Short", strategy.short)
        alert('{"SYMBOL":"' + syminfo.ticker + '","ACTION":"SELL","CHECK":"NO"}', alert.freq_once_per_bar_close)
        check := 2
    else
        strategy.close("Long")
        alert('{"SYMBOL":"' + syminfo.ticker + '","ACTION":"BUY","CHECK":"YES"}', alert.freq_once_per_bar_close)
        check := 0
if (ta.crossunder(DIPlus,DIMinus) or bonusconditionsell) and customRSI <= 0 and check == 0 and barstate.isconfirmed
    strategy.entry("Short", strategy.short)
    alert('{"SYMBOL":"' + syminfo.ticker + '","ACTION":"SELL","CHECK":"NO"}', alert.freq_once_per_bar_close)
    check := 2
if check == 2 and (ta.crossover(DIPlus,DIMinus) or customRSI > 0) and barstate.isconfirmed
    if (ta.crossover(DIPlus,DIMinus) or bonusconditionbuy) and customRSI > 0
        strategy.close("Short")
        strategy.entry("Long", strategy.long)
        alert('{"SYMBOL":"' + syminfo.ticker + '","ACTION":"BUY","CHECK":"NO"}', alert.freq_once_per_bar_close)
        check := 1
    else
        strategy.close("Short")
        alert('{"SYMBOL":"' + syminfo.ticker + '","ACTION":"SELL","CHECK":"YES"}', alert.freq_once_per_bar_close)
        check := 0
